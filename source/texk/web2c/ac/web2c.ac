## texk/web2c/ac/web2c.ac
## configure options for TeX and MF
AC_ARG_WITH([editor],
            [AS_HELP_STRING([--with-editor=CMD],
                            [invoke CMD from the `e' option @<:@vi +%d %s@:>@])])[]dnl
AC_ARG_ENABLE([auto-core],
              [AS_HELP_STRING([--enable-auto-core],
                              [cause TeX&MF to dump core, given a certain filename])])[]dnl
AC_ARG_ENABLE([dump-share],
              [AS_HELP_STRING([--disable-dump-share],
                              [make fmt/base/mem files architecture-dependent])])[]dnl
AC_ARG_ENABLE([fmtutil],
              [AS_HELP_STRING([--enable-fmtutil],
                              [use fmtutil for build dumps])])[]dnl
## configure options for TeX
AC_ARG_ENABLE([ipc],
              [AS_HELP_STRING([--enable-ipc],
                              [enable TeX's --ipc option, i.e., pipe to a program])])[]dnl
m4_define([kpse_tex_progs], [dnl
[[tex],    [yes], [no],  [TeX],    []],
[[etex],   [no],  [yes], [e-TeX],  []],
[[omega],  [no],  [no],  [Omega],  []],
[[aleph],  [yes], [no],  [Aleph],  []],
[[pdftex], [yes], [yes], [pdfTeX], [xpdf libpng obsdcompat]],
[[luatex], [yes], [no],  [luaTeX], [xpdf libpng obsdcompat]],
[[xetex],  [no],  [yes], [XeTeX],  [xpdf libpng freetype2]],
])[]dnl
m4_foreach([Kpse_Prog], [kpse_tex_progs],
           [m4_ifset([Kpse_Prog],
                     [KPSE_WITH_XTEX(Kpse_Prog)])])[]dnl
## configure options for MF
AC_ARG_ENABLE([mf],
              [AS_HELP_STRING([--disable-mf],
                              [do not build METAFONT])])[]dnl
AC_ARG_ENABLE([mf-nowin],
              [AS_HELP_STRING([--enable-mf-nowin],
                              [build a separate non-windows-capable METAFONT],
                              [30])])[]dnl
AC_ARG_WITH([mf-x-toolkit],
            [AS_HELP_STRING([--with-mf-x-toolkit],
                            [Use X toolkit for METAFONT])])[]dnl
m4_define([kpse_mf_win], [dnl
[[epsf],      [EPSF],      [EPSF pseudo]],
[[hp2627],    [HP2627],    [HP 2627]],
[[mftalk],    [MFTALK],    [mftalk (generic server)]],
[[next],      [NEXT],      [NeXT]],
[[regis],     [REGIS],     [Regis]],
[[suntools],  [SUN],       [old Suntools (this is not X)]],
[[tektronix], [TEKTRONIX], [Tektronix]],
[[uniterm],   [UNITERM],   [Uniterm]],
])[]dnl
m4_foreach([Kpse_Win], [kpse_mf_win],
           [m4_ifset([Kpse_Win],
                     [KPSE_WITH_MFWIN(Kpse_Win)])])[]dnl
## configure options for MP
AC_ARG_ENABLE([mp],
              [AS_HELP_STRING([--disable-mp],
                              [do not build METAPOST])])[]dnl
## configure options for WEB programs
AC_ARG_ENABLE([web-progs],
              [AS_HELP_STRING([--disable-web-progs],
                              [do not build WEB programs bibtex ... weave])])[]dnl
